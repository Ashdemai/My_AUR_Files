# Maintainer: zxp19821005 <zxp19821005 at 163 dot com>
pkgname="drawio-desktop-appimage"
pkgver=21.2.8
pkgrel=1
pkgdesc="Diagram drawing application built on web technology"
arch=('x86_64')
url="https://www.diagrams.net/"
_githuburl="https://github.com/jgraph/drawio-desktop"
license=('Apache')
options=(!strip)
conflicts=("${pkgname%-appimage}-desktop")
depends=('zlib' 'hicolor-icon-theme' 'glibc')
_install_path="/opt/appimages"
source=("${pkgname%-appimage}-${pkgver}.AppImage::${_githuburl}/releases/download/v${pkgver}/${pkgname%-desktop-appimage}-x86_64-${pkgver}.AppImage")
sha256sums=('c5b25300e35399330d1f1cc38d5c4ed426c0727e8cdd9c02de999f753e8f9c56')
prepare() {
    chmod a+x "${pkgname%-appimage}-${pkgver}.AppImage"
    "./${pkgname%-appimage}-${pkgver}.AppImage" --appimage-extract > /dev/null
    sed 's|AppRun|/opt/appimages/drawio-desktop.AppImage|g;s|Icon=drawio|Icon=drawio-desktop|g' -i "${srcdir}/squashfs-root/${pkgname%-desktop-appimage}.desktop"
}
package() {
    install -Dm755 "${srcdir}/${pkgname%-appimage}-${pkgver}.AppImage" "${pkgdir}/${_install_path}/${pkgname%-appimage}.AppImage"
    for _icons in 16x16 32x32 48x48 64x64 96x96 128x128 192x192 256x256 512x512 720x720 1024x1024;do
        install -Dm644 "${srcdir}/squashfs-root/usr/share/icons/hicolor/${_icons}/apps/${pkgname%-desktop-appimage}.png" \
            "${pkgdir}/usr/share/icons/hicolor/${_icons}/apps/${pkgname%-appimage}.png"
    done
    install -Dm644 "${srcdir}/squashfs-root/${pkgname%-desktop-appimage}.desktop" "${pkgdir}/usr/share/applications/${pkgname%-appimage}.desktop"
    install -Dm644 "${srcdir}/squashfs-root/usr/share/mime/${pkgname%-desktop-appimage}.xml" -t "${pkgdir}/usr/share/applications/mime"
}
