# Maintainer: zxp19821005 <zxp19821005 at 163 dot com>
pkgname="wubi-dict-editor"
pkgver=1.18
pkgrel=1
pkgdesc="五笔码表助手 for Rime ( Windows、macOS、Ubuntu ) 基于 electron 开发."
arch=("x86_64")
url="https://github.com/KyleBing/wubi-dict-editor"
license=('GPL3')
depends=('cairo' 'gcc-libs' 'mesa' 'nss' 'libxrandr' 'libcups' 'gtk3' 'nodejs' 'libxshmfence' 'libxfixes' 'expat' \
    'pango' 'hicolor-icon-theme' 'alsa-lib' 'libxcomposite' 'at-spi2-core' 'dbus' 'nspr' 'libx11' 'libxext' 'libxcb' \
    'glib2' 'gdk-pixbuf2' 'libxdamage' 'libdrm' 'libxkbcommon' 'glibc')
provides=("KyleBing")
conflicts=("${pkgname}")
noextract=("${pkgname}-icons.tar.gz")
options=(!strip)
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/refs/tags/v${pkgver}.tar.gz"
    "${pkgname}-icons.tar.gz")
sha256sums=('6826e76cec7acd07414d337fd52f77d6f77c78472154901fae793a75180dbef1'
            '7ff0ada13d432ace0493e8436431d812da97a2661b86c18e62abdc9e54723076')
build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    npm i
    yarn run package
}
package() {
    install -Dm755 -d "${pkgdir}/opt/${pkgname}"
    cp -r "${srcdir}/${pkgname}-${pkgver}/out/五笔码表助手-linux-x64/"* "${pkgdir}/opt/${pkgname}"
    mv "${pkgdir}/opt/${pkgname}/五笔码表助手" "${pkgdir}/opt/${pkgname}/${pkgname}"
    bsdtar -xf "${srcdir}/${pkgname}-icons.tar.gz" -C "${pkgdir}/opt/${pkgname}/resources" --gname root --uname root
    for _icons in 16x16 32x32 256x256 512x512 1024x1024; do
        install -Dm644 "${pkgdir}/opt/${pkgname}/resources/${pkgname}_${_icons/x*}.png" \
            "${pkgdir}/usr/share/icons/hicolor/${_icons}/apps/${pkgname}.png"
    done
    gendesk -f --pkgname "${pkgname}" --pkgdesc "五笔码表助手 for Rime ( Windows、macOS、Ubuntu ) 基于 electron 开发." --icon "${pkgname}" \
        --categories "Utility" --name "${pkgname}" --genericname="五笔码表助手 for Rime" --exec "/opt/${pkgname}/${pkgname}"
    install -Dm644 "${srcdir}/${pkgname}.desktop" -t "${pkgdir}/usr/share/applications"
}