# Maintainer: zxp19821005 <zxp19821005 at 163 dot com>
pkgname="ferdium-appimage"
pkgver=6.2.6
pkgrel=1
pkgdesc="All your services in one place, built by the community"
arch=('any')
url="https://ferdium.org/"
_githuburl="https://github.com/ferdium/ferdium-app"
license=('Apache')
conflicts=("${pkgname%-appimage}" "${pkgname%-appimage}-electron" "${pkgname%-appimage}-nightly")
depends=('zlib' 'hicolor-icon-theme' 'glibc')
options=(!strip)
_install_path="/opt/appimages"
source=("${pkgname%-appimage}-${pkgver}.AppImage::${_githuburl}/releases/download/v${pkgver}/Ferdium-linux-Portable-${pkgver}-x86_64.AppImage")
sha256sums=('2f8d2d82177f5168bf55ab7c6b69f70e3d1689c87cb4127a050b990d01f26b09')
   
prepare() {
    chmod a+x "${pkgname%-appimage}-${pkgver}.AppImage"
    "./${pkgname%-appimage}-${pkgver}.AppImage" --appimage-extract > /dev/null
    sed 's|AppRun|/opt/appimages/ferdium.AppImage|g' -i "${srcdir}/squashfs-root/${pkgname%-appimage}.desktop"
}
   
package() {
    install -Dm755 "${srcdir}/${pkgname%-appimage}-${pkgver}.AppImage" "${pkgdir}/${_install_path}/${pkgname%-appimage}.AppImage"
    for icons in 16x16 24x24 32x32 48x48 64x64 96x96 128x128 256x256 512x512 1024x1024; do
        install -Dm644 "${srcdir}/squashfs-root/usr/share/icons/hicolor/${icons}/apps/${pkgname%-appimage}.png" \
            -t "${pkgdir}/usr/share/icons/hicolor/${icons}/apps"
    done
    install -Dm644 "${srcdir}/squashfs-root/${pkgname%-appimage}.desktop" -t "${pkgdir}/usr/share/applications"
}