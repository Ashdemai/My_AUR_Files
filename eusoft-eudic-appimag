# Maintainer: zxp19821005 <zxp19821005 at 163 dot com>
_appname=eudic
pkgname="eusoft-${_appname}-appimage"
pkgver=2023.06.06
pkgrel=1
pkgdesc="权威的英语词典软件,英语学习者必备的工具,支持学习笔记、生词本多平台同步,让你随时随地学英语."
arch=('x86_64')
url="https://www.eudic.net/v4/en/app/eudic"
license=('custom')
provides=("${_appname}=${pkgver}")
conflicts=("${pkgname%-appimage}" "${_appname}")
options=("!strip")
_install_path="/opt/appimages"
depends=('glibc' 'zlib')
source=("${pkgname%-appimage}-${pkgver}.AppImage::https://www.eudic.net/download/${_appname}.AppImage?v=${pkgver//./-}"
    "LICENSE.html")
sha256sums=('feb5c16a657533f2a28e34d2ac971b17c9cc4e56f6bf2e46a3e20a8da4c11db2'
            'ebb5a622b0bef6435db80733d78253a0995bcbe9b8f9703914e841121c0fef8c')
prepare() {
    chmod a+x "${srcdir}/${pkgname%-appimage}-${pkgver}.AppImage"
    "${srcdir}/${pkgname%-appimage}-${pkgver}.AppImage" --appimage-extract > /dev/null
    sed "s|/usr/share/${_appname}/AppRun %F|${_install_path}/${pkgname%-appimage}.AppImage|g;s|com.eusoft.${_appname}|${pkgname%-appimage}|g" \
        -i "${srcdir}/squashfs-root/default.desktop"
}
package() {
    install -Dm755 "${srcdir}/${pkgname%-appimage}-${pkgver}.AppImage" "${pkgdir}/${_install_path}/${pkgname%-appimage}.AppImage"
    install -Dm644 "${srcdir}/squashfs-root/com.eusoft.${_appname}.png" "${pkgdir}/usr/share/pixmaps/${pkgname%-appimage}.png"
    install -Dm644 "${srcdir}/squashfs-root/default.desktop" "${pkgdir}/usr/share/applications/${pkgname%-appimage}.desktop"
    install -Dm644 "${srcdir}/LICENSE.html" -t "${pkgdir}/usr/share/licenses/${pkgname}"
}